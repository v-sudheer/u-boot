@; ------------------------------------------------------------
@; Cortex-A MPCore - SCU functions
@;
@; Copyright ARM Ltd 2009. All rights reserved.
@; ------------------------------------------------------------

  @PRESERVE8

  @AREA  MP_SCU, CODE, READONLY

@; ------------------------------------------------------------
@; Misc
@; ------------------------------------------------------------

  .global get_base_addr
  @; unsigned int get_base_addr(void)
  @; Returns the base address of the private peripheral memory space
get_base_addr:
  MRC     p15, 4, r0, c15, c0, 0  @; Read periph base address      (see DE593076)
  BX      lr

@; ------------------------------------------------------------

  .global get_cpu_id
  @; unsigned int get_cpu_id(void)
  @; Returns the CPU ID (0 to 3) of the CPU executed on
get_cpu_id:
  MRC     p15, 0, r0, c0, c0, 5   @; Read CPU ID register
  AND     r0, r0, #0x03           @; Mask off, leaving the CPU ID field
  BX      lr

@; ------------------------------------------------------------

  .global get_num_cpus
  @; unsigned int get_num_cpus(void)
  @; Returns the number of CPUs in the A9 Cluster
get_num_cpus:

  @; Get base address of private perpherial space
  MRC     p15, 4, r0, c15, c0, 0  @; Read periph base address
  
  LDR     r0, [r0, #0x004]        @; Read SCU Configuration register
  AND     r0, r0, #0x3            @; Bits 1:0 gives the number of cores

  BX      lr
  
@; ------------------------------------------------------------

  .global go_to_sleep
  @; void go_to_sleep(void)
go_to_sleep:
  WFI                             @; Go into standby
  B       go_to_sleep             @; Catch in case of rogue events
  BX      lr

@; ------------------------------------------------------------
@; SCU
@; ------------------------------------------------------------

  @; SCU offset from base of private peripheral space --> 0x000

  .global  enable_scu
  @; void enable_scu(void)
  @; Enables the SCU
enable_scu:

  MRC     p15, 4, r0, c15, c0, 0  @; Read periph base address   reading Configuration Base address reg pg no 91 a9 trm                                                     
  LDR     r1, [r0, #0x0]          @; Read the SCU Control Register  pg 27 a9 mpcore trm                                                                                                 
  ORR     r1, r1, #0x1            @; Set bit 0 (The Enable bit)
  STR     r1, [r0, #0x0]          @; Write back modifed value

  BX      lr

@; ------------------------------------------------------------

  .global  join_smp
  @; void join_smp(void)
  @; Set this CPU as participating in SMP
join_smp:

  @; SMP status is controlled by bit 6 of the CP15 Aux Ctrl Reg

  MRC     p15, 0, r0, c1, c0, 1   @; Read ACTLR  ref to pg 76 a9 trm
  ORR     r0, r0, #0x040          @; Set bit 6
  MCR     p15, 0, r0, c1, c0, 1   @; Write ACTLR

  BX      lr

@; ------------------------------------------------------------

  .global leave_smp
  @; void leave_smp(void)
  @; Set this CPU as NOT participating in SMP
leave_smp:

  @; SMP status is controlled by bit 6 of the CP15 Aux Ctrl Reg

  MRC     p15, 0, r0, c1, c0, 1   @; Read ACTLR
  BIC     r0, r0, #0x040          @; Clear bit 6
  MCR     p15, 0, r0, c1, c0, 1   @; Write ACTLR

  BX      lr

@; ------------------------------------------------------------

  .global get_cpus_in_smp
  @; unsigned int get_cpus_in_smp(void)
  @; The return value is 1 bit per core:
  @; bit 0 - CPU 0
  @; bit 1 - CPU 1
  @; etc...
get_cpus_in_smp:

  MRC     p15, 4, r0, c15, c0, 0  @; Read periph base address

  LDR     r0, [r0, #0x004]        @; Read SCU Configuration register
  MOV     r0, r0, LSR #4          @; Bits 7:4 gives the cores in SMP mode, shift then mask
  AND     r0, r0, #0x0F

  BX      lr

@; ------------------------------------------------------------

  .global enable_maintenance_broadcast
  @; void enable_maintenance_broadcast(void)
  @; Enable the broadcasting of cache & TLB maintenance operations
  @; When enabled AND in SMP, broadcast all "inner sharable"
  @; cache and TLM maintenance operations to other SMP cores
enable_maintenance_broadcast:
  MRC     p15, 0, r0, c1, c0, 1   @; Read Aux Ctrl register  ACTLR pg 76 a9 trm                 
  ORR     r0, r0, #0x01           @; Set the FW bit (bit 0)
  MCR     p15, 0, r0, c1, c0, 1   @; Write Aux Ctrl register

  BX      lr

@; ------------------------------------------------------------

  .global disable_maintenance_broadcast
  @; void disable_maintenance_broadcast(void)
  @; Disable the broadcasting of cache & TLB maintenance operations
disable_maintenance_broadcast:
  MRC     p15, 0, r0, c1, c0, 1   @; Read Aux Ctrl register
  BIC     r0, r0, #0x01           @; Clear the FW bit (bit 0)
  MCR     p15, 0, r0, c1, c0, 1   @; Write Aux Ctrl register

  BX      lr

@; ------------------------------------------------------------

  .global secure_SCU_invalidate
  @; void secure_SCU_invalidate(unsigned int cpu, unsigned int ways)
  @; cpu: 0x0=CPU 0 0x1=CPU 1 etc...
  @; This function invalidates the SCU copy of the tag rams
  @; for the specified core.  Typically only done at start-up.
  @; Possible flow:
  @; - Invalidate L1 caches
  @; - Invalidate SCU copy of TAG RAMs
  @; - Join SMP
secure_SCU_invalidate:

	PUSH {r0-r3,lr}
  AND     r0, r0, #0x03           @; Mask off unused bits of CPU ID
  MOV     r0, r0, LSL #2          @; Convert into bit offset (four bits per core)
  
  AND     r1, r1, #0x0F           @; Mask off unused bits of ways
  MOV     r1, r1, LSL r0          @; Shift ways into the correct CPU field

  MRC     p15, 4, r2, c15, c0, 0  @; Read periph base address

  STR     r1, [r2, #0x0C]         @; Write to SCU Invalidate All in Secure State  ref to table 1-3 a9 mpcore trm, pg 31 SCU invalidate All registers in a9 mpcore trm
  
	POP {r0-r3,lr}
  BX      lr


.global enable_access
enable_access:

  MRC     p15, 4, r0, c15, c0, 0  @; Read periph base address
  LDR	  R1,=0xFFF  
/*  LDR	  R1,[R0,#0x54]
  ORR	  R1,R1,#(1<<4)
  ORR	  R1,R1,#(1<<8)
  ORR	  R1,R1,#(1<<0)*/
  STR	  R1,[R0,#0x54]
BX	lr

@; ------------------------------------------------------------
@; End of code
@; ------------------------------------------------------------


@; ------------------------------------------------------------
@; End of MP_SCU.s
@; ------------------------------------------------------------

